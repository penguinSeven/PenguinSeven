; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "paperlessCms"
#define MyAppVersion "v1.0.13"
#define MyAppPublisher "ITC"
#define MyAppURL "http://www.example.com/"
#define MyAppExeName "paperless.exe"
#define MyAppFlag "flag"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{C892CF97-477A-48FD-9168-437585227F52}
AppName={cm:MyAppName}
AppVersion={#MyAppVersion}
AppVerName={cm:MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={sd}\paperless
DisableProgramGroupPage=yes
LicenseFile=E:\upupw\doc\readme_chs.txt
OutputDir=E:\安装包
OutputBaseFilename=Paperless_Meeting_web ({#MyAppVersion})
Compression=lzma
SolidCompression=yes
ChangesEnvironment=true
Password=itcadmin
;AlwaysRestart=true

Uninstallable=yes
UninstallDisplayName={cm:Uninstall} {cm:MyAppName}

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl" ; LicenSeFile :"E:\upupw\doc\readme_en.txt"
Name: "chs"; MessagesFile: "compiler:Languages\china.isl"; LicenSeFile :"E:\upupw\doc\readme_chs.txt"

[Messages]
en.BeveledLabel=English
chs.BeveledLabel=Chineses


[CustomMessages]
en.MyAppName          = Intelligent paperless conference management server
en.Uninstall          = Uninstall
en.StartMessage       = Setup has detected that "Intelligent paperless conference management server" is running! 
en.StartMessageYes    = Click the "yes" button to close the program and continue to install;
en.StartMessageNo     = Click the "no" button to exit the installation!
en.EndMessage         = Uninstall the program to detect the "Intelligent paperless conference management server" is running! 
en.EndMessageYes      = click the "yes" button to close the program and continue to uninstall;
en.EndMessageNo       = click the "no" button  to exit the uninstall!
en.ChoseTitle         = Select installation type
en.ChoseTips          = Please select the type of installation according to your needs.
en.ChoseStandard      = Standard installation
en.ChoseUpdate        = Update
en.ChoseStandardTips  = Install the software in accordance with the standard mode to your computer, the database will update coverage.
en.ChoseUpdateTips    = Database does not cover


chs.MyAppName         = 智能无纸化会议管理服务器
chs.Uninstall         = 卸载
chs.StartMessage      = 安装程序检测到 "智能无纸化会议管理服务器" 正在运行！chs.StartMessageYes   = 单击“是”按钮关闭程序并继续安装； 
chs.StartMessageNo    = 单击“否”按钮退出安装！
chs.EndMessage        = 卸载程序检测到 "智能无纸化会议管理服务器" 正在运行
chs.EndMessageYes     = 单击“是”按钮关闭程序并继续卸载；
chs.EndMessageNo      = 单击“否”按钮退出卸载！
chs.ChoseTitle        = 选择安装类型  
chs.ChoseTips         = 请根据您的需要选择安装的类型  
chs.ChoseStandard     = 标准安装
chs.ChoseUpdate       = 更新
chs.ChoseStandardTips = 按照标准模式安装软件到您的电脑，数据库将更新覆盖。
chs.ChoseUpdateTips   = 数据库不覆盖

[ini]
Filename: "{app}\htdocs\paperless\webapp\Library\config.ini"; Section: "OTHER"; Flags:  uninsdeleteentry;
Filename: "{app}\htdocs\paperless\webapp\Library\config.ini"; Section: "OTHER"; Key: "default_language"; String: "chinese"  ; Languages: en ;
Filename: "{app}\htdocs\paperless\webapp\Library\config.ini"; Section: "OTHER"; Key: "default_language"; String: "chinese"  ; Languages: chs ;
Filename: "{app}\htdocs\paperless\webapp\Library\config.ini"; Section: "OTHER"; Key: "default_logger_path"; String: "{app}\htdocs\paperless\webapp\Temporary\Logger\"  ;
Filename: "{app}\htdocs\paperless\webapp\Library\config.ini"; Section: "OTHER"; Key: "upload_file_public"; String: "{app}htdocs\paperless\webserver\public\"  ;

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: checkablealone

[Files]
; dontcopy
Source: "E:\upupw\psvince.dll"; Flags: dontcopy
Source: "E:\upupw\doc\*"; Flags: dontcopy

; 运行环境
Source: "E:\upupw\Apache2\*"; DestDir: "{app}\Apache2\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Backup\*"; DestDir: "{app}\Backup\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\FileZillaftp\*"; DestDir: "{app}\FileZillaftp\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Guard\*"; DestDir: "{app}\Guard\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\PHP7\*"; DestDir: "{app}\PHP7\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\upcore\*"; DestDir: "{app}\upcore\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\xdebug\*"; DestDir: "{app}\xdebug\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\paperless\ServerDaemon\*"; DestDir: "{app}\paperless\ServerDaemon\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\phpmyadmin\*"; DestDir: "{app}\phpmyadmin\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\temp\*"; DestDir: "{app}\temp\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\red5-server\*"; DestDir: "{app}\red5-server\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\jdk\*"; DestDir: "{app}\jdk\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\log\*"; DestDir: "{app}\log\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\LibreOffice5\*"; DestDir: "{app}\LibreOffice5\"; Flags: ignoreversion recursesubdirs createallsubdirs

;redis 文件夹
Source: "E:\upupw\Redis\redis-service.conf"; DestDir: "{app}\Redis\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Redis\redis-server.exe"; DestDir: "{app}\Redis\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Redis\redis-cli.exe"; DestDir: "{app}\Redis\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Redis\redis-check-dump.exe"; DestDir: "{app}\Redis\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Redis\redis-check-aof.exe"; DestDir: "{app}\Redis\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Redis\redis-benchmark.exe"; DestDir: "{app}\Redis\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\Redis\EventLog.dll"; DestDir: "{app}\Redis\"; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "E:\upupw\htdocs\paperless\webapp\Application\*"; DestDir: "{app}\htdocs\paperless\webapp\Application\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\htdocs\paperless\webapp\Library\*"; DestDir: "{app}\htdocs\paperless\webapp\Library\"; Flags: ignoreversion recursesubdirs createallsubdirs

; Resource 资源文件夹
Source: "E:\upupw\htdocs\paperless\webapp\Resource\_\*"; DestDir: "{app}\htdocs\paperless\webapp\Resource\_\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\htdocs\paperless\webapp\Resource\font\*"; DestDir: "{app}\htdocs\paperless\webapp\Resource\font\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\htdocs\paperless\webapp\Resource\image\*"; DestDir: "{app}\htdocs\paperless\webapp\Resource\image\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\htdocs\paperless\webapp\Resource\js\*"; DestDir: "{app}\htdocs\paperless\webapp\Resource\js\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\htdocs\paperless\webapp\Resource\sql\*"; DestDir: "{app}\htdocs\paperless\webapp\Resource\sql\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\htdocs\paperless\webapp\Resource\template\*"; DestDir: "{app}\htdocs\paperless\webapp\Resource\template\"; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "E:\upupw\htdocs\paperless\webapp\Resource\.htaccess"; DestDir: "{app}\htdocs\paperless\webapp\Resource\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\htdocs\paperless\webapp\Resource\favicon.ico"; DestDir: "{app}\htdocs\paperless\webapp\Resource\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "E:\upupw\htdocs\paperless\webapp\Resource\index.php"; DestDir: "{app}\htdocs\paperless\webapp\Resource\"; Flags: ignoreversion recursesubdirs createallsubdirs

; C++ 后台服务器，文件服务器
Source: "E:\upupw\paperless\FileServer\FileServer.exe"; DestDir: "{app}\paperless\FileServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\FileServer\Net.dll"; DestDir: "{app}\paperless\FileServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\paperlessServer\avcodec-55.dll"; DestDir: "{app}\paperless\paperlessServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\paperlessServer\avformat-55.dll"; DestDir: "{app}\paperless\paperlessServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\paperlessServer\Microsoft.VC90.MFC.manifest"; DestDir: "{app}\paperless\paperlessServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\paperlessServer\avutil-52.dll"; DestDir: "{app}\paperless\paperlessServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\paperlessServer\mfc90.dll"; DestDir: "{app}\paperless\paperlessServer\"; Flags: ignoreversion;
Source: "E:\upupw\paperless\paperlessServer\PaperlessServer.exe"; DestDir: "{app}\paperless\paperlessServer\"; Flags: ignoreversion;

; 基础文件
Source: "E:\upupw\htdocs\favicon.ico"; DestDir: "{app}\htdocs\"; Flags: ignoreversion;
Source: "E:\upupw\paperless.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "E:\upupw\boot.cmd"; DestDir: "{app}"; Flags: ignoreversion
Source: "E:\upupw\paperlessCms.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "E:\upupw\start.cmd"; DestDir: "{app}"; Flags: ignoreversion
Source: "E:\upupw\updateSql.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "E:\upupw\ffmpeg\bin\ffmpeg.exe"; DestDir: "{app}\ffmpeg\bin\"; Flags: ignoreversion
Source: "E:\upupw\LibreOffice5\program\soffice.exe"; DestDir: "{app}\LibreOffice5\program\"; Flags: ignoreversion

; 安装复制，更新忽略
Source: "E:\upupw\MariaDB\*"; DestDir: "{app}\MariaDB\";Flags: ignoreversion recursesubdirs createallsubdirs; Check: MyProgCheck;
Source: "E:\upupw\red5-server\webapps\oflaDemo\streams\*"; DestDir: "{app}\red5-server\webapps\oflaDemo\streams\";Flags: ignoreversion recursesubdirs createallsubdirs; Check: MyProgCheck;
Source: "E:\upupw\paperless\FileServer\etc\config.xml"; DestDir: "{app}\paperless\FileServer\etc\"; Flags: ignoreversion; Check: MyProgCheck ;
Source: "E:\upupw\paperless\paperlessServer\etc\config.xml"; DestDir: "{app}\paperless\paperlessServer\etc\"; Flags: ignoreversion; Check: MyProgCheck;
Source: "E:\upupw\htdocs\paperless\webapp\Resource\key.token"; DestDir: "{app}\htdocs\paperless\webapp\Resource\"; Flags: ignoreversion ; Check: MyProgCheck;

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commonprograms}\{cm:MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:MyAppName}"; Filename: "{app}\{#MyAppExeName}";
Name: "{commondesktop}\{cm:MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{group}\Uninstall {cm:MyAppName}"; Filename: "{uninstallexe}"

[Registry]
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "upupw"; ValueData: "{app}\{#MyAppExeName}"
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "startServer"; ValueData: "{app}\start.cmd"
Root: HKLM; Subkey: Software\The Application; ValueType: string; ValueName: "{#MyAppFlag}"; ValueData: '2.0'; Flags: uninsdeletekey


[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "{app}\boot.cmd"; Flags: nowait runhidden
Filename: "{app}\updateSql.exe"; Flags: nowait runhidden ;Check: MyProgCheck1;
Filename: "{app}\red5-server\install-service.exe"; Flags: runhidden ;

[UninstallRun]
Filename: "{app}\red5-server\red5-shutdown.bat"; Flags: nowait runhidden
Filename: "{app}\red5-server\uninstall-service.bat"; Flags: nowait runhidden
Filename: "{cmd}"; Parameters: "/c rd /s /q ""{app}"""; Flags: hidewizard runhidden

[UninstallDelete]
Name: {app}; Type: filesandordirs

[Code]
function IsModuleLoaded(modulename: AnsiString ):  Boolean;
external 'IsModuleLoaded@files:psvince.dll stdcall';

var
Page: TWizardPage;
RadioButton1, RadioButton2: TRadioButton;
Lbl1, Lbl2: TNewStaticText;
TaskCode: Boolean;

// PSVince控件在64位系统（Windows 7/Server 2008/Server 2012）下无法检测到进程，使用下面的函数可以解决。
function IsAppRunning(const FileName : string): Boolean;
var
    FSWbemLocator: Variant;
    FWMIService   : Variant;
    FWbemObjectSet: Variant;
begin
    Result := false;
    try
      FSWbemLocator := CreateOleObject('WBEMScripting.SWBEMLocator');
      FWMIService := FSWbemLocator.ConnectServer('', 'root\CIMV2', '', '');
      FWbemObjectSet := FWMIService.ExecQuery(Format('SELECT Name FROM Win32_Process Where Name="%s"',[FileName]));
      Result := (FWbemObjectSet.Count > 0);
      FWbemObjectSet := Unassigned;
      FWMIService := Unassigned;
      FSWbemLocator := Unassigned;
    except
      if (IsModuleLoaded(FileName)) then
        begin
          Result := false;
        end
      else
        begin
          Result := true;
        end
      end;
end;

  // 关闭进程
function TaskKillProcessByName(const FileName : string): Boolean;
  var
  ResultCode: Integer;
begin
    Exec(ExpandConstant('taskkill.exe'), '/f /im ' + '"' + FileName + '"', '', SW_HIDE,
     ewWaitUntilTerminated, ResultCode);
end;

  // 卸载serverDaemon服务
function TaskShutDownDaemon(): Boolean;
  var
  ResultCode: Integer;
begin
     Exec(ExpandConstant('{app}\paperless\ServerDaemon\ServerDaemon.exe'), ' -d ', '', SW_SHOW,
     ewWaitUntilTerminated, ResultCode)
end;

 // 停止服务
function TaskStopService(const FileName : string): Boolean;
  var
  ResultCode: Integer;
begin
    Exec(ExpandConstant('net.exe'), ' stop ' + '"' + FileName + '"', '', SW_HIDE,
     ewWaitUntilTerminated, ResultCode);
end;

 // 开启服务
function TaskStartService(const FileName : string): Boolean;
  var
  ResultCode: Integer;
begin
    Exec(ExpandConstant('net.exe'), ' start ' + '"' + FileName + '"', '', SW_HIDE,
     ewWaitUntilTerminated, ResultCode);
end;

// 验证按钮
function MyProgCheck(): Boolean;
begin
  Result := RadioButton1.Checked;
end;

function MyProgCheck1(): Boolean;
begin
  Result := RadioButton2.Checked;
end;

// 执行程序
function TaskStartShell(FileName: String): Boolean;
  var
  ResultCode: Integer;
begin
    Exec(ExpandConstant(FileName), '', '', SW_HIDE,
     ewWaitUntilTerminated, ResultCode);
end;

// 设置环境变量函数
procedure SetEnv(aEnvName, aEnvValue: string; aIsInstall, aIsInsForAllUser: Boolean);
var
sOrgValue: string;
S1, sFileName: string;
bRetValue, bInsForAllUser: Boolean;
SL: TStringList;
x: integer;
begin
bInsForAllUser := aIsInsForAllUser;
if UsingWinNT then
begin
    if bInsForAllUser then
      bRetValue := RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', aEnvName, sOrgValue)
    else
      bRetValue := RegQueryStringValue(HKEY_CURRENT_USER, 'Environment', aEnvName, sOrgValue)
    sOrgValue := Trim(sOrgValue);
    begin
      S1 := aEnvValue;
      if pos(Uppercase(s1), Uppercase(sOrgValue)) = 0 then //还没有加入
      begin
        if aIsInstall then
        begin
          x := Length(sOrgValue);
          if (x > 0) and (StringOfChar(sOrgValue[x], 1) <> ';') then
            sOrgValue := sOrgValue + ';';
          sOrgValue := sOrgValue + S1;
        end;
      end else
      begin
        if not aIsInstall then
        begin
          StringChangeEx(sOrgValue, S1 + ';', '', True);
          StringChangeEx(sOrgValue, S1, '', True);
        end;
      end;

      if bInsForAllUser then
        RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', aEnvName, sOrgValue)
      else
      begin
        if (not aIsInstall) and (Trim(sOrgValue) = '') then
          RegDeleteValue(HKEY_CURRENT_USER, 'Environment', aEnvName)
        else
          RegWriteStringValue(HKEY_CURRENT_USER, 'Environment', aEnvName, sOrgValue);
      end;
    end;
end else //非NT 系统,如Win98
begin
    SL := TStringList.Create;
    try
      sFileName := ExpandConstant('{sd}\autoexec.bat');
      LoadStringFromFile(sFileName, S1);
      SL.Text := s1;
      s1 :=   '"' + aEnvValue + '"';
      s1 := 'set '+aEnvName +'=%path%;' + s1 ;

      bRetValue := False;
      x := SL.IndexOf(s1);
      if x = -1 then
      begin
        if aIsInstall then
        begin
          SL.Add(s1);
          bRetValue := True;
        end;
      end else //还没添加
        if not aIsInstall then
        begin
          SL.Delete(x);
          bRetValue := True;
        end;

      if bRetValue then
        SL.SaveToFile(sFileName);
    finally
      SL.free;
    end;

end;
end;


//事件函数，安装，并在安装前检测程序是否在运行，如果运行先结束进程
function InitializeSetup(): Boolean;
begin
  Result := true;
  if  IsAppRunning('httpd.exe') then
  begin
    if MsgBox(ExpandConstant('{cm:StartMessage}') + #13#13 + ExpandConstant('{cm:StartMessageYes}')+ #13#13 + ExpandConstant('{cm:StartMessageNo}'), mbConfirmation, MB_YESNO) = IDYES then
    begin

      TaskKillProcessByName('{#MyAppExeName}');
      TaskStopService('UPUPW_Apache');
      Result:= true;

      if  IsAppRunning('php.exe') then
      begin
          TaskKillProcessByName('php.exe');
      end;

      if  IsAppRunning('paperlessCms.exe') then
      begin
          TaskKillProcessByName('paperlessCms.exe');
      end;

      if  IsAppRunning('ffmpeg.exe') then
      begin
          TaskKillProcessByName('ffmpeg.exe');
      end;

      if  IsAppRunning('redis-server.exe') then
      begin
          TaskStopService('upupw_redis_a');
      end;

      if  IsAppRunning('ServerDaemon.exe') then
      begin
          TaskStopService('ServerDaemon');
      end;

      if  IsAppRunning('prunsrv.exe') then
      begin
          TaskStopService('Red5 Media Server');
      end;
    end
    else
      Result:= false;
  end;

  
end;

// 事件函数 供了安装过程中的状态
procedure CurStepChanged(CurStep: TSetupStep);
begin
       if CurStep=ssinstall then

           // 将{app}路径添加到path环境变量中
           //在这儿调用,一定在这儿调用,安装完无须重启,立即生效
           SetEnv('path',ExpandConstant('{app}\LibreOffice5\program\'),true,true); 
           SetEnv('path',ExpandConstant('{app}\ffmpeg\bin\'),true,true); 

          if  (IsAppRunning('mysqld.exe')) AND (RadioButton1.Checked) then
          begin
              TaskStopService('UPUPW_Database_A');
          end;
       if CurStep=ssPostinstall then

       if CurStep=ssDone then
       begin
          if RadioButton2.Checked then
          begin
            TaskStartService('UPUPW_Database_A');
          end;
       end;
end;

//事件函数，卸载，与安装类似
function InitializeUninstall(): Boolean;
  begin
    Result:= true;
    if  IsAppRunning('httpd.exe') then
    begin
      if MsgBox(ExpandConstant('{cm:EndMessage}')+ #13#13 + ExpandConstant('{cm:EndMessageYes}')+ #13#13 + ExpandConstant('{cm:EndMessageNo}'), mbConfirmation, MB_YESNO) = IDYES then
      begin
        TaskKillProcessByName('{#MyAppExeName}');
        TaskKillProcessByName('ffmpeg.exe');
        TaskKillProcessByName('php.exe');
        TaskKillProcessByName('paperlessCms.exe');
        TaskShutDownDaemon();
        TaskStopService('UPUPW_Apache');
        TaskStopService('upupw_redis_a');
        TaskStopService('UPUPW_Database_A');
        TaskStopService('Red5 Media Server');
        Result:= true;
      end
      else
        Result:= false;
    end;
  end;

// 定义安装类型选择界面
procedure CreateAddonPage;
begin
  Page := CreateCustomPage(wpInfoBefore, ExpandConstant('{cm:ChoseTitle}'), ExpandConstant('{cm:ChoseTips}'));

  RadioButton1 := TRadioButton.Create(Page);
  RadioButton1.Left := ScaleX(45);
  RadioButton1.Top := ScaleY(40);
  RadioButton1.Width := Page.SurfaceWidth;
  RadioButton1.Height := ScaleY(17);
  RadioButton1.Caption := ExpandConstant('{cm:ChoseStandard}');
  RadioButton1.Checked := True;
  RadioButton1.Parent := Page.Surface;

  Lbl1 := TNewStaticText.Create(Page);
  Lbl1.Left := ScaleX(55);
  Lbl1.Top := ScaleY(60);
  Lbl1.Width := ScaleX(250);
  Lbl1.Height := ScaleY(50);
  Lbl1.Caption := ExpandConstant('{cm:ChoseStandardTips}');
  Lbl1.Parent := Page.Surface;

  RadioButton2 := TRadioButton.Create(Page);
  RadioButton2.Left := ScaleX(45);
  RadioButton2.Top := RadioButton1.Top + ScaleY(60);
  RadioButton2.Width := Page.SurfaceWidth;
  RadioButton2.Height := ScaleY(17);
  RadioButton2.Caption := ExpandConstant('{cm:ChoseUpdate}');
  RadioButton2.Checked := false;
  RadioButton2.Parent := Page.Surface;

  Lbl2 := TNewStaticText.Create(Page);
  Lbl2.Left := ScaleX(55);
  Lbl2.Top := Lbl1.Top + ScaleY(60);
  Lbl2.Width := ScaleX(250);
  Lbl2.Height := ScaleY(50);
  Lbl2.Caption := ExpandConstant('{cm:ChoseUpdateTips}');
  Lbl2.Parent := Page.Surface;
end;

// 判断本程序是否已安装
function GetInstalledVersion(): Boolean;
var
    InstalledVersion: String;
begin
    InstalledVersion :='';
    RegQueryStringValue(HKLM, 'Software\The Application', '{#MyAppFlag}', InstalledVersion);

    if length(InstalledVersion) > 0 then
    begin
        result := false;
    end else begin
        result := true;
    end
end;


// 事件函数 初始化
procedure InitializeWizard();
begin
  CreateAddonPage;
end;

// 界面显示判断
function ShouldSkipPage(PageID: Integer): Boolean;
begin
  if (PageID = wpSelectTasks) and (RadioButton2.Checked) then
    Result := True
  else if (PageID = wpSelectDir)  and (RadioButton2.Checked) then
    Result := True
  else if (PageID = wpInfoBefore)  and (GetInstalledVersion) then
    Result := True
end;

// 卸载前删除环境变量
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  // 将{app}路径从path环境变量中删除
  SetEnv('path',ExpandConstant('{app}\LibreOffice5\program\'),false,true);
  SetEnv('path',ExpandConstant('{app}\ffmpeg\bin\'),false,true);
end;







